group = 'com.github.heneke.thymeleaf'
version = '1.2.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

afterEvaluate {
	project.archivesBaseName = 'thymeleaf-extras-togglz'
}

repositories { 
	mavenLocal()
	mavenCentral()
}

project.ext {
	appName = "Thymeleaf Extras Togglz ${project.version}"
	thymeleafVersion = "2.1.3.RELEASE"
	togglzVersion = "2.3.0.Final"
	ossrh = project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')
	sign = project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')
}

dependencies {
	compile "org.thymeleaf:thymeleaf:${thymeleafVersion}"
	compile "org.togglz:togglz-core:${togglzVersion}"
	testCompile "junit:junit:4.8.2"
	testCompile "org.togglz:togglz-testing:${togglzVersion}"
	testCompile "org.thymeleaf:thymeleaf-testing:${thymeleafVersion}"
}

def testBeforeRun = [install, assemble]
if(ossrh) {
	testBeforeRun << uploadArchives
}
testBeforeRun.each {
	it.dependsOn 'test'
}

if(ossrh) {
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { deployment -> signing.signPom(deployment) }
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
			}
		}
	}
}

def pomTasks = [install.repositories.mavenInstaller]
if(ossrh) {
	pomTasks << uploadArchives.repositories.mavenDeployer
}

pomTasks.each {
	configure(it) {
		pom.project {
			packaging 'jar'
			name 'Thymeleaf Extras Togglz'
			description 'Thymeleaf dialect integrating Togglz feature toggles.'
			inceptionYear '2013'
			url 'https://github.com/heneke/thymeleaf-extras-togglz'
			scm {
				connection 'https://github.com/heneke/thymeleaf-extras-togglz.git'
				developerConnection 'https://github.com/heneke/thymeleaf-extras-togglz.git'
				url 'https://github.com/heneke/thymeleaf-extras-togglz/tree/master'
			}
			licenses {
				license {
					name 'The Apache License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				}
			}
			developers {
				developer {
					id 'heneke'
					name 'Hendrik Heneke'
					email 'heneke@gmail.com'
				}
			}
			issueManagement {
				url 'https://github.com/heneke/thymeleaf-extras-togglz/issues'
				system 'Github Issues'
			}
		}
	}
}

if(sign) {
	configure(signing) {
		sign configurations.archives
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

javadoc {
	options {
		docTitle = appName
		windowTitle = appName
		charSet = "ISO-8859-1"
		links("http://www.thymeleaf.org/apidocs/thymeleaf/${thymeleafVersion}")
	}
}
